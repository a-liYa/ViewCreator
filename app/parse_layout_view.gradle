import groovy.util.slurpersupport.GPathResult
import groovy.util.slurpersupport.Node

def creator_view_ignores = [:]
ext.creator_view_ignores = creator_view_ignores

project.afterEvaluate {
    Arrays.asList("mergeDebugResources", "mergeReleaseResources").each {
        def mergeResourcesTask = project.tasks.findByName(it)
        if (mergeResourcesTask != null) {
            def parseTask = project.tasks.create("$it#LayoutParseTask", LayoutResourcesParseTask.class)
            parseTask.inputFiles = mergeResourcesTask.inputs.files
            parseTask.ignoreViewNames.addAll(creator_view_ignores.values())
            mergeResourcesTask.finalizedBy(parseTask)
        }
    }
}

class LayoutResourcesParseTask extends DefaultTask {

    File viewNamesFile
    FileCollection inputFiles
    HashSet<String> viewSet = new HashSet<>()
    final List<String> ignoreViewNames = new ArrayList<>();

    static final String LAYOUT_PATH = "layout"

    LayoutResourcesParseTask() {
        ignoreViewNames.addAll(
                Arrays.asList("include", "fragment", "merge", "view", "DateTimeView",
                        "com.google.android.material.datepicker.MaterialCalendarGridView"))
    }

    @TaskAction
    void doTask() {
        File distDir = new File(project.buildDir, "tmp/mergeResourcesLayoutParse")
        if (!distDir.exists())
            distDir.mkdirs()

        viewNamesFile = new File(distDir, "view_names.txt")
        if (viewNamesFile.exists()) {
            viewNamesFile.delete()
        }
        viewNamesFile.createNewFile()

        viewSet.clear()
        viewSet.addAll(ignoreViewNames)

        // 遍历所有输入文件夹， 拼接 /layout 子文件
        inputFiles.each { inputFile ->
            if (!inputFile.isDirectory()) return

            def layoutDir = new File(inputFile, LAYOUT_PATH)
            if (!layoutDir.exists()) return

            // 遍历所有 inputFile/layout 里面的 layout xml 并解析
            layoutDir.listFiles().each { layoutFile ->

                if (!layoutFile.name.startsWith(".")) { // 过滤隐藏文件
                    GPathResult result = new XmlSlurper().parse(layoutFile)

                    String viewName = result.name()
                    if (viewSet.add(viewName)) { // 滤重
                        viewNamesFile.append("${viewName}\n")
                    }
                    // 解析根布局下的子节点 View
                    result.childNodes().forEachRemaining({ o ->
                        if (o instanceof Node) {
                            parseViewNode(o)
                        }
                    })
                }
            }
        }
    }

    void parseViewNode(Node node) {
        String viewName = node.name()
        if (viewSet.add(viewName)) {
            viewNamesFile.append("${viewName}\n")
        }
        node.childNodes().forEachRemaining({ o ->
            if (o instanceof Node) {
                parseViewNode(o)
            }
        })
    }
}